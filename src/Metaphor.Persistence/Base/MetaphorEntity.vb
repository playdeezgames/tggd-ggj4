Imports Metaphor.Provision
Imports TGGD.Persistence
Imports TGGD.Provision

Friend MustInherit Class MetaphorEntity(Of TData As EntityData)
    Inherits Entity(Of TData)
    Implements IMetaphorEntity

    Protected Sub New(world As IWorld, data As WorldData, entityId As Guid)
        Me.World = world
        Me._data = data
        Me.EntityId = entityId
    End Sub

    Public MustOverride Sub Remove() Implements IMetaphorEntity.Remove
    Public ReadOnly Property World As IWorld Implements IMetaphorEntity.World

    Public ReadOnly Property Name As String Implements IMetaphorEntity.Name
        Get
            Return TryGetMetadata(Metadatas.NAME)
        End Get
    End Property

    Public ReadOnly Property Flavor As String Implements IMetaphorEntity.Flavor
        Get
            Return TryGetMetadata(Metadatas.FLAVOR)
        End Get
    End Property

    Public ReadOnly Property EntityId As Guid Implements IMetaphorEntity.EntityId

    Public ReadOnly Property EntitySubtype As String Implements IMetaphorEntity.EntitySubtype
        Get
            Return TryGetMetadata(Metadatas.ENTITY_SUBTYPE)
        End Get
    End Property

    Public MustOverride ReadOnly Property Exists As Boolean Implements IMetaphorEntity.Exists
    Protected ReadOnly _data As WorldData

    Public ReadOnly Property Verbs As IEnumerable(Of IVerb) Implements IMetaphorEntity.Verbs
        Get
            Return GetYokage(Yokages.VERBS).Select(Function(x) Verb.Create(World, _data, x))
        End Get
    End Property


    Public Function CreateVerb(
                              entitySubtype As String,
                              name As String,
                              flavor As String,
                              Optional initializer As VerbInitializer = Nothing) As IVerb Implements IMetaphorEntity.CreateVerb
        Dim verbId = Guid.NewGuid
        _data.Entities(verbId) = New EntityData With
            {
                .EntityType = EntityTypes.VERB_ENTITY,
                .Metadatas = New Dictionary(Of String, String) From
                {
                    {Metadatas.ENTITY_SUBTYPE, entitySubtype},
                    {Metadatas.NAME, name},
                    {Metadatas.FLAVOR, flavor}
                }
            }
        AddToYokage(Yokages.VERBS, verbId)
        Dim result As IVerb = Verb.Create(World, _data, verbId)
        initializer?.Invoke(result)
        Return result
    End Function

    Public Sub AddMessage(text As String, Optional hints As IDictionary(Of String, String) = Nothing) Implements IMetaphorEntity.AddMessage
        World.AddMessage(text, hints)
    End Sub

    Public ReadOnly Property Inventory As IInventory Implements IMetaphorEntity.Inventory
        Get
            Dim inventoryId As Guid
            If Not Data.Yokes.TryGetValue(Yokes.INVENTORY, inventoryId) Then
                inventoryId = Guid.NewGuid
                _data.Entities(inventoryId) = New EntityData
                Data.Yokes(Yokes.INVENTORY) = inventoryId
            End If
            Return Persistence.Inventory.Create(World, _data, inventoryId)
        End Get
    End Property
End Class
