Imports System.Runtime.CompilerServices
Imports Metaphor.Persistence

Friend Module CharacterExtensions
    <Extension>
    Private Function IsAvatar(character As ICharacter) As Boolean
        Return If(character.World.Avatar?.EntityId = character.EntityId, False)
    End Function
    <Extension>
    Friend Sub Look(character As ICharacter)
        Dim world = character.World
        If character.IsDead Then
            world.AddMessage($"{character.Name} is dead.")
            Return
        End If
        Dim location = character.Location
        World.AddMessage(location.Flavor)
        location.Describe()
        ShowOtherCharacters(character)
        ShowFeatures(character)
        If location.Inventory.HasItems Then
            world.AddMessage("There are items on the ground.")
        End If
    End Sub

    <Extension>
    Friend Sub ShowOtherCharacters(character As ICharacter)
        Dim others = character.Location.GetOtherCharacters(character)
        If others.Any Then
            character.World.AddMessage("Characters:")
            For Each other In others
                character.World.AddMessage($"- {other.Name}")
            Next
        End If
    End Sub

    <Extension>
    Friend Sub ShowFeatures(character As ICharacter)
        Dim features = character.Location.Features
        If features.Any Then
            character.World.AddMessage($"Features:")
            For Each feature In features
                character.World.AddMessage($"- {feature.Name}")
            Next
        End If
    End Sub
    <Extension>
    Friend Sub ShowStatus(character As ICharacter)
        Dim world = character.World
        world.AddMessage($"{character.Name}'s Status:")
        world.AddMessage(character.Flavor)
        world.AddMessage($"Health: {character.GetCounterStatistic(Counters.HEALTH)}")
        world.AddMessage($"Satiety: {character.GetCounterStatistic(Counters.SATIETY)}")
        world.AddMessage($"Stomach: {character.GetCounterStatistic(Counters.STOMACH)}")
        world.AddMessage($"Jools: {character.GetJools():f2}")
    End Sub
    <Extension>
    Friend Function GetHealth(character As ICharacter) As Integer
        Return character.GetCounter(Counters.HEALTH)
    End Function
    <Extension>
    Friend Function GetSatiety(character As ICharacter) As Integer
        Return character.GetCounter(Counters.SATIETY)
    End Function
    <Extension>
    Friend Function GetStomach(character As ICharacter) As Integer
        Return character.GetCounter(Counters.STOMACH)
    End Function
    <Extension>
    Friend Function GetMaximumHealth(character As ICharacter) As Integer
        Return character.GetCounterMaximum(Counters.HEALTH)
    End Function
    <Extension>
    Friend Function GetMaximumSatiety(character As ICharacter) As Integer
        Return character.GetCounterMaximum(Counters.SATIETY)
    End Function
    <Extension>
    Friend Function GetMaximumStomach(character As ICharacter) As Integer
        Return character.GetCounterMaximum(Counters.STOMACH)
    End Function
    <Extension>
    Friend Sub DoBiology(character As ICharacter, amount As Integer)
        character.ApplyBreathing(amount)
        character.ApplyHunger(amount)
    End Sub
    <Extension>
    Private Sub ApplyBreathing(character As ICharacter, amount As Integer)
        Dim ship = character.GetShip()
        If character.Location.EntityId = ship.EntityId AndAlso
            Not ship.IsSnorkelRaised Then
            Dim oxygenAvailable = ship.GetOxygen()
            Dim damage = Math.Max(0, amount - oxygenAvailable)
            ship.ChangeDimension(Dimensions.OXYGEN, -amount)
            If damage > 0 Then
                Dim world = character.World
                world.AddMessage($"{character.Name} is asphyxiating!")
                character.ApplyDamage(CInt(damage))
            End If
        End If
    End Sub
    <Extension>
    Private Sub ApplyDamage(character As ICharacter, damage As Integer)
        If Not character.IsDead() AndAlso damage > 0 Then
            Dim world = character.World
            world.AddMessage($"{character.Name} takes {damage} damage!")
            character.ChangeCounter(Counters.HEALTH, -damage)
            If character.IsDead Then
                world.AddMessage($"{character.Name} dies.")
            Else
                world.AddMessage($"{character.Name} has {character.GetHealth()}/{character.GetMaximumHealth()} health left!")
            End If
        End If
    End Sub
    <Extension>
    Private Sub ApplyHunger(character As ICharacter, amount As Integer)
        If character.IsDead Then
            Return
        End If
        Dim world = character.World
        Dim stomach = Math.Min(character.GetStomach(), amount)
        amount -= stomach
        If stomach > 0 Then
            world.AddMessage($"{character.Name}'s stomach goes down by {stomach}.")
            character.ChangeCounter(Counters.STOMACH, -stomach)
            world.AddMessage($"{character.Name} now has a stomach of {character.GetStomach}/{character.GetMaximumStomach}.")
            stomach = Math.Min(stomach, character.GetMaximumSatiety - character.GetSatiety)
            If stomach > 0 Then
                world.AddMessage($"{character.Name}'s satiety goes up by {stomach}.")
                character.ChangeCounter(Counters.SATIETY, stomach)
                world.AddMessage($"{character.Name} now has a satiety of {character.GetSatiety}/{character.GetMaximumSatiety}.")
            End If
        End If
        Dim satiety = Math.Min(character.GetSatiety(), amount)
        amount -= satiety
        If satiety > 0 Then
            world.AddMessage($"{character.Name}'s satiety goes down by {satiety}.")
            character.ChangeCounter(Counters.SATIETY, -satiety)
            world.AddMessage($"{character.Name} now has a satiety of {character.GetSatiety}/{character.GetMaximumSatiety}.")
        End If
        Dim health = Math.Min(character.GetHealth(), amount)
        amount -= health
        If health > 0 Then
            world.AddMessage($"{character.Name}'s health goes down by {health}.")
            character.ChangeCounter(Counters.HEALTH, -health)
            world.AddMessage($"{character.Name} now has a health of {character.GetHealth}/{character.GetMaximumHealth}.")
        End If
    End Sub
    <Extension>
    Friend Function IsDead(character As ICharacter) As Boolean
        Return character.IsCounterMinimum(Counters.HEALTH)
    End Function
    <Extension>
    Friend Sub SetShip(character As ICharacter, ship As ILocation)
        character.SetYoke(Yokes.SHIP, ship.EntityId)
    End Sub
    <Extension>
    Friend Function GetShip(character As ICharacter) As ILocation
        Dim identifier = character.GetYoke(Yokes.SHIP)
        Return If(identifier.HasValue, character.World.GetLocation(identifier.Value), Nothing)
    End Function
    <Extension>
    Friend Sub AddKnownBubble(character As ICharacter, bubble As ILocation)
        character.AddToYokage(Yokages.KNOWN_BUBBLES, bubble.EntityId)
    End Sub
    <Extension>
    Friend Sub SetMode(character As ICharacter, mode As String)
        character.SetMetadata(Metadatas.MODE, mode)
    End Sub
    <Extension>
    Friend Function GetMode(character As ICharacter) As String
        Return If(character.TryGetMetadata(Metadatas.MODE), String.Empty)
    End Function
    <Extension>
    Friend Sub ClearMode(character As ICharacter)
        character.SetMetadata(Metadatas.MODE, Nothing)
    End Sub
    <Extension>
    Friend Sub BuyFuel(character As ICharacter, units As Double)
        Dim ship = character.GetShip()
        Dim price = character.Location.GetFuelingStation().GetFuelPrice()
        units = {units, ship.GetFuelCapacity(), character.GetJools() / price}.Min()
        Dim jools = units * price
        character.AddMessage($"{character.Name} buys {units:f2} fuel @ {price:f2} jools/unit.")
        ship.Refuel(units)
        character.AddMessage($"{ship} now has {ship.GetDimensionStatistic(Dimensions.FUEL)} fuel.")
        character.ChangeDimension(Dimensions.JOOLS, -jools)
        character.AddMessage($"{character.Name} now has {character.GetJools():f2} jools")
    End Sub
End Module
